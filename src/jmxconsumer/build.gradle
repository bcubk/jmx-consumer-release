buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.google.protobuf' version '0.8.1'
}

apply plugin: 'org.junit.platform.gradle.plugin'
group 'org.cloudfoundry'
version '1.0-SNAPSHOT'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.9.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.junitJupiterVersion  = '5.0.3'
ext.junitPlatformVersion = '1.0.3'

dependencies {
    compile 'io.grpc:grpc-all:1.9.0'
    compile 'org.bouncycastle:bcprov-jdk15on:1.59'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.59'
    compile 'io.netty:netty-codec-http2:4.1.17.Final'
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.7.Final'
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.10.0'
    compile 'org.apache.logging.log4j:log4j-core:2.10.0'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.4'

    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testCompile "org.junit.platform:junit-platform-console-standalone:${junitPlatformVersion}"
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
    testCompile group: 'com.j256.simplejmx', name: 'simplejmx', version: '1.15'
    testCompile "org.mockito:mockito-core:2.+"
    testCompile 'org.apache.httpcomponents:httpclient:4.5.5'
}

mainClassName = "org.cloudfoundry.jmxconsumer.App"
jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude ('META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA') //https://stackoverflow.com/a/45153151
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/resources/loggregator-api/v2'
        }
    }
}

test {
    testLogging.showStandardStreams = true
    dependsOn "assemble"
}

junitPlatform {
    details 'verbose'
    filters {
        tags {
            exclude 'compatibility'
        }
    }
}

task compatibilityTest(dependsOn: compileTestJava, type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main 'org.junit.platform.console.ConsoleLauncher'
    systemProperties System.getProperties()
    args = [
            '--select-method', 'integration.CompatibilityTest#customMetricsFromVMS',
            ]
}