buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.google.protobuf' version '0.8.1'
}

apply plugin: 'org.junit.platform.gradle.plugin'
group 'org.cloudfoundry'
version '1.0-SNAPSHOT'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.9.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.junitJupiterVersion  = '5.0.3'
ext.junitPlatformVersion = '1.0.0'

dependencies {
    compile 'io.grpc:grpc-all:1.9.0'
    compile 'io.netty:netty-codec-http2:4.1.17.Final'
    compile 'io.netty:netty-tcnative-boringssl-static:2.0.7.Final'
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
    testCompile group: 'com.j256.simplejmx', name: 'simplejmx', version: '1.15'
}

mainClassName = "org.cloudfoundry.jmxnozzle.App"
jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/resources/loggregator-api/v2'
        }
    }
}

test {
    testLogging.showStandardStreams = true
    dependsOn "assemble"
}

junitPlatform {
    filters {
        tags {
            exclude 'compatibility'
        }
    }
}